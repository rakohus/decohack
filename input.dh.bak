//
// This source file generates a DeHackEd file using DECOhack.
//
// https://mtrop.github.io/DoomTools/
//

#include <mbf21>

// Quality of life change for masterminds.
thing SpiderMastermind "Spiderdemon (QoL)"
{
    radius 96
}

thing MT_EXTRA00 "Aracnorb (Valiant)"
{
    clear properties
    ednum 90
    health 120
    speed 12
    radius 24
    height 56
    damage 0
    reactiontime 8
    painchance 150
    mass 400

    clear flags
    +SOLID
    +SHOOTABLE
    +JUSTATTACKED
    +NOGRAVITY
    +FLOAT
    +COUNTKILL

    clear sounds
    seesound "FRE000"
    painsound "DMPAIN"
    deathsound "FRE001"
    activesound "BSPACT"

    clear states
    states
    {
    spawn:
        SP00 A 10 A_Look
        goto spawn
    see:
        SP00 A 3 A_Chase
        SP00 A 3 A_Chase
        goto see
    pain:
        SP00 H 3
        SP00 H 3 A_Pain
        goto see
    missile:
        SP00 A 20 A_FaceTarget
        SP00 BCB 9 bright A_BspiAttack
        SP00 A 9 A_SpidRefire
        goto missile
    raise:
        SP00 FED 5
        goto see
    death:
        SP00 D 5
        SP00 E 5 A_Scream
        SP00 F 5 A_Fall
        SP00 G -1
        stop
    }
}

thing MT_EXTRA01 "Afrit (DVII)"
{
    clear properties
    ednum 91
    health 1500
    speed 8
    radius 20
    height 64
    damage 0
    reactiontime 5
    painchance 30
    mass 400

    clear flags
    +SOLID
    +SHOOTABLE
    +NOGRAVITY
    +FLOAT
    +COUNTKILL

    clear sounds
    seesound "BRSSIT"
    painsound "DMPAIN"
    deathsound "BRSDTH"
    activesound "DMACT"

    clear states
    states
    {
    spawn:
        SP01 A 10 bright A_Look
        loop
    see:
        SP01 ABCD 3 bright A_Chase
        loop
    pain:
        SP01 H 8 bright
        goto see
    missile:
        SP01 EF 6 bright A_FaceTarget
        SP01 G 1 bright A_FatAttack1
        SP01 G 1 bright A_FatAttack2
        SP01 G 1 bright A_FatAttack3
        SP01 G 1 bright A_FaceTarget
        SP01 G 1 bright A_SkelMissile
        SP01 G 1 bright A_SkelMissile
        SP01 G 8 bright A_SkelMissile
        goto see
    death:
        SP01 I 5 bright
        SP01 J 5 bright A_Scream
        SP01 K 5 bright
        SP01 L 5 bright A_Fall
        SP01 MNOPQ 5 bright
        SP01 R 5 bright A_BossDeath
        SP01 R 1 bright
        stop
    }
}

thing MT_EXTRA02 : thing Cyberdemon "Mini-Cyberdemon (Darkest Room)"
{
    ednum 92
    health 1000

    states
    {
    spawn:
        SP02 AB 10 A_Look
        loop
    see:
        SP02 A 3 A_Hoof
        SP02 ABBCC 3 A_Chase
        SP02 D 3 A_Metal
        SP02 D 3 A_Chase
        loop
    missile:
        SP02 E 6 A_FaceTarget
        SP02 F 12 A_CyberAttack
        SP02 E 12 A_FaceTarget
        SP02 F 12 A_CyberAttack
        SP02 E 12 A_FaceTarget
        SP02 F 12 A_CyberAttack
        goto see
    pain:
        SP02 G 10 A_Pain
        goto see
    death:
        SP02 H 10
        SP02 I 10 A_Scream
        SP02 JKL 10
        SP02 M 10 A_Fall
        SP02 NO 10
        SP02 P 30
        SP02 P -1 A_BossDeath
        stop
    }
}

thing MT_EXTRA03 "Stealth Plasma Alien (Ancient Aliens)"
{
    clear properties
    ednum 93
    health 30
    speed 14
    radius 20
    height 56
    damage 0
    reactiontime 8
    painchance 170
    mass 100

    clear flags
    +SOLID
    +SHOOTABLE
    +COUNTKILL

    clear sounds
    seesound "FRE002"
    painsound "POPAIN"
    deathsound "FRE003"
    activesound "FRE004"

    clear states
    states
    {
    spawn:
        SP03 NO 10 A_Look
        loop
    see:
        SP03 AABBCCDD 2 A_Chase
        loop
    pain:
        SP03 H 3
        SP03 H 3 A_Pain
        goto see
    missile:
        SP03 EF 10 bright A_FaceTarget
        SP03 G 4 bright A_BspiAttack
        SP03 F 4 A_FaceTarget
        SP03 G 4 bright A_BspiAttack
        SP03 F 0
        goto see
    death:
        SP03 I 5
        SP03 J 5 A_Scream
        SP03 K 5 A_Fall
        SP03 L 5
        SP03 M -1
        stop
    raise:
        SP03 MLKJI 5
        goto see
    }
}

thing MT_EXTRA04 "Suicide Bomber (Valiant)"
{
    clear properties
    ednum 94
    health 60
    speed 10
    radius 20
    height 56
    damage 96
    reactiontime 8
    painchance 5
    mass 100

    clear flags
    +SOLID
    +SHOOTABLE
    +COUNTKILL

    clear sounds
    seesound "POSIT2"
    painsound "POPAIN"

    clear states
    states
    {
    spawn:
        SP04 DE 10 A_Look
        loop
    see:
        // This sound will randomly appear on quit.
        SP04 G 0 A_PlaySound(95) // DSBOSCUB
        SP04 DDDEEEFFFGGG 2 A_Chase
        loop
    pain:
        SP04 D 3
        SP04 D 3 A_Pain
        goto see
    melee:
        SP04 D 0 bright A_Detonate
        SP04 A 5 bright A_PlaySound(82) // DSBAREXP
        SP04 B 5 bright
        SP04 C 5 A_Fall
        SP04 H 5 A_XScream
        SP04 IJK 5
        SP04 L -1
        stop
    death:
        TNT1 A 0
        goto melee
    }
}

thing MT_EXTRA05 "PowerSlave Tree"
{
    ednum 10603
    radius 16
    height 160

    +SOLID

    states
    {
    spawn:
        SP05 A -1
        stop
    }
}

thing MT_EXTRA06 "Crop 1" 
{
    ednum 12400
    radius 16
    height 40

    states
    {
    spawn:
        SP06 A -1
        stop
    }
}

thing MT_EXTRA07 "Crop 2" 
{
    ednum 12401
    radius 16
    height 40

    states
    {
    spawn:
        SP06 B -1
        stop
    }
}

thing MT_EXTRA07 "Giant Imp"
{
    ednum 95
    health 600
    speed 15
    radius 40
    height 112
    damage 96
    reactiontime 8
    painchance 400
    mass 800
	meleerange 32

    clear flags
    +SOLID
    +SHOOTABLE
    +COUNTKILL
	
//	clear sounds
    seesound "BGSIT1"
    painsound "POPAIN"
  	deathsound "BGDTH"
  	activesound "BGACT"

    states
    {
    spawn:
        SP07 AB 10 A_Look
        loop
    see:
        SP07 AABBCCDD 3 A_Chase
        loop
	Melee:
		SP07 E 8
		SP08 F 6 A_MonsterMeleeAttack(50, 0, sound "claw")
		goto see
    Missile:
        SP07 EF 8 A_FaceTarget
        SP07 G 6 BRIGHT A_MonsterProjectile(10, 0, 0, 0, -30)
        SP07 EF 6 A_FaceTarget
        SP07 G 6 BRIGHT A_MonsterProjectile(10, 0, 0, 0, -30)
        goto see
    Pain:
        SP07 G 10 A_Pain
        goto see
    Death:
        SP07 H 10
        SP07 I 10 A_Scream
        SP07 JKL 10
        SP07 M 10 A_Fall
        SP07 NO 10
        SP07 P 30 A_Mushroom
        SP07 P -1 A_BossDeath
        stop
    }
}

//class FatShot : Actor
//{
//	Default
//	{
//		Radius 6;
//		Height 8;
//		Speed 25;
//		Damage 8;
//		Projectile;
//		+RANDOMIZE
//		+RIP
//		SeeSound "FIRSHT"
//		DeathSound "FIRXPL"
//	}
//	States
//	{
//	Spawn:
//		MANF AB 4 BRIGHT;
//		Loop;
//	Death:
//		MISL B 8 BRIGHT;
//		MISL C 6 BRIGHT;
//		MISL D 4 BRIGHT;
//		Stop;
//	}
//}